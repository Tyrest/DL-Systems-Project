#include <cuda_runtime.h>
#include <pybind11/numpy.h>
#include <pybind11/pybind11.h>
#include <pybind11/stl.h>

#include <iostream>
#include <sstream>
#include <vector>

namespace needle {
namespace cuda {

#define BASE_THREAD_NUM 256

#define TILE 4
typedef float scalar_t;
const size_t ELEM_SIZE = sizeof(scalar_t);

struct CudaArray {
  CudaArray(const size_t size) {
    cudaError_t err = cudaMalloc(&ptr, size * ELEM_SIZE);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));
    this->size = size;
  }
  ~CudaArray() { cudaFree(ptr); }
  size_t ptr_as_int() { return (size_t)ptr; }
  
  scalar_t* ptr;
  size_t size;
};

struct CudaDims {
  dim3 block, grid;
};

CudaDims CudaOneDim(size_t size) {
  /**
   * Utility function to get cuda dimensions for 1D call
   */
  CudaDims dim;
  size_t num_blocks = (size + BASE_THREAD_NUM - 1) / BASE_THREAD_NUM;
  dim.block = dim3(BASE_THREAD_NUM, 1, 1);
  dim.grid = dim3(num_blocks, 1, 1);
  return dim;
}

#define MAX_VEC_SIZE 8
struct CudaVec {
  uint32_t size;
  int32_t data[MAX_VEC_SIZE];
};

CudaVec VecToCuda(const std::vector<int32_t>& x) {
  CudaVec shape;
  if (x.size() > MAX_VEC_SIZE) throw std::runtime_error("Exceeded CUDA supported max dimesions");
  shape.size = x.size();
  for (size_t i = 0; i < x.size(); i++) {
    shape.data[i] = x[i];
  }
  return shape;
}

////////////////////////////////////////////////////////////////////////////////
// Fill call
////////////////////////////////////////////////////////////////////////////////

__global__ void FillKernel(scalar_t* out, scalar_t val, size_t size) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = val;
}

void Fill(CudaArray* out, scalar_t val) {
  CudaDims dim = CudaOneDim(out->size);
  FillKernel<<<dim.grid, dim.block>>>(out->ptr, val, out->size);
}

////////////////////////////////////////////////////////////////////////////////
// Compact and setitem cals
////////////////////////////////////////////////////////////////////////////////

// Untility function to convert contiguous index i to memory location from strides
__device__ size_t GetMemoryLocation(const CudaVec& shape, const CudaVec& strides, size_t i) {
  size_t idx = 0;
  for (size_t j = 0; j < strides.size; j++) {
    size_t dim = shape.size - 1 - j;
    idx += (i % shape.data[dim]) * strides.data[dim];
    i /= shape.data[dim];
  }
  return idx;
}

__global__ void CompactKernel(const scalar_t* a, scalar_t* out, size_t size, CudaVec shape,
                              CudaVec strides, size_t offset) {
  /**
   * The CUDA kernel for the compact opeation.  This should effectively map a single entry in the 
   * non-compact input a, to the corresponding item (at location gid) in the compact array out.
   * 
   * Args:
   *   a: CUDA pointer to a array
   *   out: CUDA point to out array
   *   size: size of out array
   *   shape: vector of shapes of a and out arrays (of type CudaVec, for past passing to CUDA kernel)
   *   strides: vector of strides of out array
   *   offset: offset of out array
   */
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;

  /// BEGIN SOLUTION
  if (gid < size) {
    out[gid] = a[GetMemoryLocation(shape, strides, gid) + offset];
  }
  /// END SOLUTION
}

void Compact(const CudaArray& a, CudaArray* out, std::vector<int32_t> shape,
             std::vector<int32_t> strides, size_t offset) {
  /**
   * Compact an array in memory.  Unlike the C++ version, in CUDA this will primarily call the 
   * relevant CUDA kernel.  In this case, we illustrate how you should set this up (i.e., we give 
   * you the code for this fuction, and also the prototype for the CompactKernel() function).  For
   * the functions after this, however, you'll need to define these kernels as you see fit to 
   * execute the underlying function.
   * 
   * Args:
   *   a: non-compact represntation of the array, given as input
   *   out: compact version of the array to be written
   *   shape: shapes of each dimension for a and out
   *   strides: strides of the *a* array (not out, which has compact strides)
   *   offset: offset of the *a* array (not out, which has zero offset, being compact)
   */

  // Nothing needs to be added here
  CudaDims dim = CudaOneDim(out->size);
  CompactKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, VecToCuda(shape),
                                         VecToCuda(strides), offset);
}



__global__ void EwiseSetitemKernel(const scalar_t* a, scalar_t* out, size_t size, CudaVec shape,
                                   CudaVec strides, size_t offset) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) {
    out[GetMemoryLocation(shape, strides, gid) + offset] = a[gid];
  }
}

void EwiseSetitem(const CudaArray& a, CudaArray* out, std::vector<int32_t> shape,
                  std::vector<int32_t> strides, size_t offset) {
  /**
   * Set items in a (non-compact) array using CUDA.  Yyou will most likely want to implement a
   * EwiseSetitemKernel() function, similar to those above, that will do the actual work.
   * 
   * Args:
   *   a: _compact_ array whose items will be written to out
   *   out: non-compact array whose items are to be written
   *   shape: shapes of each dimension for a and out
   *   strides: strides of the *out* array (not a, which has compact strides)
   *   offset: offset of the *out* array (not a, which has zero offset, being compact)
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(a.size);
  EwiseSetitemKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, a.size, VecToCuda(shape),
                                              VecToCuda(strides), offset);
  /// END SOLUTION
}



__global__ void ScalarSetitemKernel(size_t size, scalar_t val, scalar_t* out, CudaVec shape,
                                    CudaVec strides, size_t offset) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) {
    out[GetMemoryLocation(shape, strides, gid) + offset] = val;
  }
}

void ScalarSetitem(size_t size, scalar_t val, CudaArray* out, std::vector<int32_t> shape,
                   std::vector<int32_t> strides, size_t offset) {
  /**
   * Set items is a (non-compact) array
   * 
   * Args:
   *   size: number of elements to write in out array (note that this will note be the same as
   *         out.size, because out is a non-compact subset array);  it _will_ be the same as the 
   *         product of items in shape, but covenient to just pass it here.
   *   val: scalar value to write to
   *   out: non-compact array whose items are to be written
   *   shape: shapes of each dimension of out
   *   strides: strides of the out array
   *   offset: offset of the out array
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(out->size);
  ScalarSetitemKernel<<<dim.grid, dim.block>>>(size, val, out->ptr, VecToCuda(shape),
                                               VecToCuda(strides), offset);
  /// END SOLUTION
}

////////////////////////////////////////////////////////////////////////////////
// Elementwise and scalar operations
////////////////////////////////////////////////////////////////////////////////


__global__ void EwiseAddKernel(const scalar_t* a, const scalar_t* b, scalar_t* out, size_t size) {
  // Calculate the global index of the thread.
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = a[gid] + b[gid];
}

void EwiseAdd(const CudaArray& a, const CudaArray& b, CudaArray* out) {
  /**
   * Add together two CUDA arrays.
   * Args:
   *   a: Input array 'a' to be added
   *   b: Input array 'b' to be added
   *   out: Output array to store the result of 'a + b'
   */
  CudaDims dim = CudaOneDim(out->size);

  // Kernel will execute on 'dim.grid' blocks, each containing 'dim.block' threads.
  EwiseAddKernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size);
}

__global__ void ScalarAddKernel(const scalar_t* a, scalar_t val, scalar_t* out, size_t size) {
  // Calculate the global index of the thread.
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) out[gid] = a[gid] + val;
}

void ScalarAdd(const CudaArray& a, scalar_t val, CudaArray* out) {
  /**
   * Add a scalar value to every element of a CUDA array.
   * Args:
   *   a: Input array 'a'
   *   val: Scalar value to be added
   *   out: Output array to store the result of 'a + val'
   */
  CudaDims dim = CudaOneDim(out->size);

  // Launch the ScalarAddKernel that will add the scalar 'val' to each element of array 'a', 
  // and store the result in array 'out'.
  ScalarAddKernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size);
}

/**
 * In the code the follows, use the above template to create analogous elementise
 * and and scalar operators for the following functions.  See the numpy backend for
 * examples of how they should work.
 *   - EwiseMul, ScalarMul
 *   - EwiseDiv, ScalarDiv
 *   - ScalarPower
 *   - EwiseMaximum, ScalarMaximum
 *   - EwiseEq, ScalarEq
 *   - EwiseGe, ScalarGe
 *   - EwiseLog
 *   - EwiseExp
 *   - EwiseTanh
 *
 * If you implement all these naively, there will be a lot of repeated code, so
 * you are welcome (but not required), to use macros or templates to define these
 * functions (however you want to do so, as long as the functions match the proper)
 * signatures above.
 */


////////////////////////////////////////////////////////////////////////////////
// Elementwise and scalar operations
////////////////////////////////////////////////////////////////////////////////

#define EWISE_BINARY_OP(name, expr) \
__global__ void Ewise##name##Kernel(const scalar_t* a, const scalar_t* b, scalar_t* out, size_t size) { \
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x; \
  if (gid < size) out[gid] = expr(a[gid], b[gid]); \
} \
void Ewise##name(const CudaArray& a, const CudaArray& b, CudaArray* out) { \
  CudaDims dim = CudaOneDim(out->size); \
  Ewise##name##Kernel<<<dim.grid, dim.block>>>(a.ptr, b.ptr, out->ptr, out->size); \
}

#define SCALAR_BINARY_OP(name, expr) \
__global__ void Scalar##name##Kernel(const scalar_t* a, scalar_t val, scalar_t* out, size_t size) { \
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x; \
  if (gid < size) out[gid] = expr(a[gid], val); \
} \
void Scalar##name(const CudaArray& a, scalar_t val, CudaArray* out) { \
  CudaDims dim = CudaOneDim(out->size); \
  Scalar##name##Kernel<<<dim.grid, dim.block>>>(a.ptr, val, out->ptr, out->size); \
}

#define EWISE_UNARY_OP(name, expr) \
__global__ void Ewise##name##Kernel(const scalar_t* a, scalar_t* out, size_t size) { \
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x; \
  if (gid < size) out[gid] = expr(a[gid]); \
} \
void Ewise##name(const CudaArray& a, CudaArray* out) { \
  CudaDims dim = CudaOneDim(out->size); \
  Ewise##name##Kernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size); \
}

#define OP_MUL(a, b) ((a) * (b))
#define OP_DIV(a, b) ((a) / (b))
#define OP_POW(a, b) (pow(a, b))
#define OP_MAX(a, b) (max(a, b))
#define OP_EQ(a, b) ((a) == (b))
#define OP_GE(a, b) ((a) >= (b))
#define OP_LOG(a) (log(a))
#define OP_EXP(a) (exp(a))
#define OP_TANH(a) (tanh(a))

EWISE_BINARY_OP(Mul, OP_MUL)
SCALAR_BINARY_OP(Mul, OP_MUL)

EWISE_BINARY_OP(Div, OP_DIV)
SCALAR_BINARY_OP(Div, OP_DIV)

SCALAR_BINARY_OP(Power, OP_POW)

EWISE_BINARY_OP(Maximum, OP_MAX)
SCALAR_BINARY_OP(Maximum, OP_MAX)

EWISE_BINARY_OP(Eq, OP_EQ)
SCALAR_BINARY_OP(Eq, OP_EQ)

EWISE_BINARY_OP(Ge, OP_GE)
SCALAR_BINARY_OP(Ge, OP_GE)

EWISE_UNARY_OP(Log, OP_LOG)
EWISE_UNARY_OP(Exp, OP_EXP)
EWISE_UNARY_OP(Tanh, OP_TANH)


#define MATMUL_S 8
#define MATMUL_L 8
#define MATMUL_V 2

__device__ size_t idx2d(size_t y, size_t x, size_t cols) {
  return y * cols + x;
}

__device__ void LoadTileToShared(const scalar_t* A, const scalar_t* B, scalar_t* sA, scalar_t* sB,
                                 size_t yblock, size_t xblock, size_t k, size_t tid, size_t nthreads,
                                 uint32_t M, uint32_t N, uint32_t P) {
  for(int j = 0; j < MATMUL_L * MATMUL_S / nthreads; ++j) {
    size_t y = (j * nthreads + tid) / MATMUL_L;
    size_t x = (j * nthreads + tid) % MATMUL_L;
    
    size_t row_a = yblock * MATMUL_L + y;
    size_t col_a = k + x;
    sA[idx2d(y, x, MATMUL_L)] = (row_a < M && col_a < N) ? A[idx2d(row_a, col_a, N)] : 0;
    
    size_t row_b = k + y;
    size_t col_b = xblock * MATMUL_L + x;
    sB[idx2d(y, x, MATMUL_L)] = (row_b < N && col_b < P) ? B[idx2d(row_b, col_b, P)] : 0;
  }
}

__device__ void LoadRegistersFromShared(const scalar_t* sA, const scalar_t* sB, scalar_t* a, scalar_t* b,
                                        size_t ki) {
  for (size_t y = 0; y < MATMUL_V; ++y) {
    a[y] = sA[idx2d(threadIdx.y * MATMUL_V + y, ki, MATMUL_L)];
    b[y] = sB[idx2d(ki, threadIdx.x * MATMUL_V + y, MATMUL_L)];
  }
}

__device__ void ComputeOuterProduct(const scalar_t* a, const scalar_t* b, scalar_t* c) {
  for (size_t y = 0; y < MATMUL_V; ++y) {
    for (size_t x = 0; x < MATMUL_V; ++x) {
      c[idx2d(y, x, MATMUL_V)] += a[y] * b[x];
    }
  }
}

__device__ void WriteResultstoOut(scalar_t* out, const scalar_t* c, size_t yblock, size_t xblock,
                                  uint32_t M, uint32_t P) {
  size_t ybase = yblock * MATMUL_L + threadIdx.y * MATMUL_V;
  size_t xbase = xblock * MATMUL_L + threadIdx.x * MATMUL_V;
  
  for (size_t y = 0; y < MATMUL_V; ++y) {
    for (size_t x = 0; x < MATMUL_V; ++x) {
      size_t row = ybase + y;
      size_t col = xbase + x;
      if (row < M && col < P) {
        out[idx2d(row, col, P)] = c[idx2d(y, x, MATMUL_V)];
      }
    }
  }
}

__global__ void MatmulKernel(const scalar_t* A, const scalar_t* B, scalar_t* out, uint32_t M,
                             uint32_t N, uint32_t P) {
  __shared__ scalar_t sA[MATMUL_S * MATMUL_L], sB[MATMUL_S * MATMUL_L];
  scalar_t c[MATMUL_V * MATMUL_V] = {0};
  scalar_t a[MATMUL_V], b[MATMUL_V];
  
  size_t yblock = blockIdx.y;
  size_t xblock = blockIdx.x;
  size_t nthreads = blockDim.y * blockDim.x;
  size_t tid = threadIdx.y * blockDim.x + threadIdx.x;

  for (size_t k = 0; k < N; k += MATMUL_S) {
    __syncthreads();
    LoadTileToShared(A, B, sA, sB, yblock, xblock, k, tid, nthreads, M, N, P);
    __syncthreads();
    for (size_t ki = 0; ki < MATMUL_S; ++ki) {
      LoadRegistersFromShared(sA, sB, a, b, ki);
      ComputeOuterProduct(a, b, c);
    }
  }
  WriteResultstoOut(out, c, yblock, xblock, M, P);
}

void Matmul(const CudaArray& a, const CudaArray& b, CudaArray* out, uint32_t M, uint32_t N,
            uint32_t P) {
  /**
   * Multiply two (compact) matrices into an output (also comapct) matrix.  You will want to look
   * at the lecture and notes on GPU-based linear algebra to see how to do this.  Since ultimately
   * mugrade is just evaluating correctness, you _can_ implement a version that simply parallelizes
   * over (i,j) entries in the output array.  However, to really get the full benefit of this
   * problem, we would encourage you to use cooperative fetching, shared memory register tiling, 
   * and other ideas covered in the class notes.  Note that unlike the tiled matmul function in
   * the CPU backend, here you should implement a single function that works across all size
   * matrices, whether or not they are a multiple of a tile size.  As with previous CUDA
   * implementations, this function here will largely just set up the kernel call, and you should
   * implement the logic in a separate MatmulKernel() call.
   * 
   *
   * Args:
   *   a: compact 2D array of size m x n
   *   b: comapct 2D array of size n x p
   *   out: compact 2D array of size m x p to write the output to
   *   M: rows of a / out
   *   N: columns of a / rows of b
   *   P: columns of b / out
   */

  /// BEGIN SOLUTION
  dim3 blockDim(MATMUL_L / MATMUL_V, MATMUL_L / MATMUL_V);
  dim3 gridDim((P + MATMUL_L - 1) / MATMUL_L, (M + MATMUL_L - 1) / MATMUL_L);
  MatmulKernel<<<gridDim, blockDim>>>(a.ptr, b.ptr, out->ptr, M, N, P);
  /// END SOLUTION
}

////////////////////////////////////////////////////////////////////////////////
// Max and sum reductions
////////////////////////////////////////////////////////////////////////////////

__global__ void ReduceMaxKernel(const scalar_t* a, scalar_t* out, size_t size, size_t reduce_size) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) {
    out[gid] = a[gid * reduce_size];
    for (size_t i = 1; i < reduce_size; i++) {
     out[gid] = max(out[gid], a[gid * reduce_size + i]);
    }
  }
}

void ReduceMax(const CudaArray& a, CudaArray* out, size_t reduce_size) {
  /**
   * Reduce by taking maximum over `reduce_size` contiguous blocks.  Even though it is inefficient,
   * for simplicity you can perform each reduction in a single CUDA thread.
   * 
   * Args:
   *   a: compact array of size a.size = out.size * reduce_size to reduce over
   *   out: compact array to write into
   *   redice_size: size of the dimension to reduce over
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(out->size);
  ReduceMaxKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, reduce_size);
  /// END SOLUTION
}



__global__ void ReduceSumKernel(const scalar_t* a, scalar_t* out, size_t size, size_t reduce_size) {
  size_t gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid < size) {
    out[gid] = 0;
    for (size_t i = 0; i < reduce_size; i++) {
     out[gid] += a[gid * reduce_size + i];
    }
  }
}

void ReduceSum(const CudaArray& a, CudaArray* out, size_t reduce_size) {
  /**
   * Reduce by taking summation over `reduce_size` contiguous blocks.  Again, for simplicity you 
   * can perform each reduction in a single CUDA thread.
   * 
   * Args:
   *   a: compact array of size a.size = out.size * reduce_size to reduce over
   *   out: compact array to write into
   *   redice_size: size of the dimension to reduce over
   */
  /// BEGIN SOLUTION
  CudaDims dim = CudaOneDim(out->size);
  ReduceSumKernel<<<dim.grid, dim.block>>>(a.ptr, out->ptr, out->size, reduce_size);
  /// END SOLUTION
}

// Simple int8 matmul kernel: int8 x int8 -> int32 accumulation, rescale to float
__global__ void MatmulInt8Kernel(const int8_t* a, const int8_t* b, float* out, int64_t m,
                                 int64_t k, int64_t n, float a_scale, float b_scale) {
  int row = blockIdx.y * blockDim.y + threadIdx.y;
  int col = blockIdx.x * blockDim.x + threadIdx.x;
  if (row < m && col < n) {
    int32_t acc = 0;
    for (int kk = 0; kk < k; kk++) {
      acc += static_cast<int32_t>(a[row * k + kk]) * static_cast<int32_t>(b[kk * n + col]);
    }
    out[row * n + col] = static_cast<float>(acc) * a_scale * b_scale;
  }
}

}  // namespace cuda
}  // namespace needle

PYBIND11_MODULE(ndarray_backend_cuda, m) {
  namespace py = pybind11;
  using namespace needle;
  using namespace cuda;

  m.attr("__device_name__") = "cuda";
  m.attr("__tile_size__") = TILE;

  py::class_<CudaArray>(m, "Array")
      .def(py::init<size_t>(), py::return_value_policy::take_ownership)
      .def_readonly("size", &CudaArray::size)
      .def("ptr", &CudaArray::ptr_as_int);

  // return numpy array, copying from CPU
  m.def("to_numpy", [](const CudaArray& a, std::vector<size_t> shape, std::vector<size_t> strides,
                       size_t offset) {
    std::vector<size_t> numpy_strides = strides;
    std::transform(numpy_strides.begin(), numpy_strides.end(), numpy_strides.begin(),
                   [](size_t& c) { return c * ELEM_SIZE; });

    // copy memory to host
    scalar_t* host_ptr = (scalar_t*)std::malloc(a.size * ELEM_SIZE);
    if (host_ptr == 0) throw std::bad_alloc();
    cudaError_t err = cudaMemcpy(host_ptr, a.ptr, a.size * ELEM_SIZE, cudaMemcpyDeviceToHost);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));

    // return numpy array
    py::capsule deallocate_buffer(host_ptr, [](void* p) { free(p); });
    return py::array_t<scalar_t>(shape, numpy_strides, host_ptr + offset, deallocate_buffer);
  });

  // copy numpy array to GPU
  m.def("from_numpy", [](py::array_t<scalar_t> a, CudaArray* out) {
    cudaError_t err =
        cudaMemcpy(out->ptr, a.request().ptr, out->size * ELEM_SIZE, cudaMemcpyHostToDevice);
    if (err != cudaSuccess) throw std::runtime_error(cudaGetErrorString(err));
  });

  m.def("fill", Fill);
  m.def("compact", Compact);
  m.def("ewise_setitem", EwiseSetitem);
  m.def("scalar_setitem", ScalarSetitem);
  m.def("ewise_add", EwiseAdd);
  m.def("scalar_add", ScalarAdd);

  m.def("ewise_mul", EwiseMul);
  m.def("scalar_mul", ScalarMul);
  m.def("ewise_div", EwiseDiv);
  m.def("scalar_div", ScalarDiv);
  m.def("scalar_power", ScalarPower);

  m.def("ewise_maximum", EwiseMaximum);
  m.def("scalar_maximum", ScalarMaximum);
  m.def("ewise_eq", EwiseEq);
  m.def("scalar_eq", ScalarEq);
  m.def("ewise_ge", EwiseGe);
  m.def("scalar_ge", ScalarGe);

  m.def("ewise_log", EwiseLog);
  m.def("ewise_exp", EwiseExp);
  m.def("ewise_tanh", EwiseTanh);

  m.def("matmul", Matmul);

  m.def("reduce_max", ReduceMax);
  m.def("reduce_sum", ReduceSum);

  // Naive int8 matmul on CUDA: copies inputs to device, runs kernel, copies result back.
  m.def(
      "matmul_int8",
      [](py::array_t<int8_t, py::array::c_style | py::array::forcecast> a,
         py::array_t<int8_t, py::array::c_style | py::array::forcecast> b,
         float a_scale, float b_scale) {
        auto a_buf = a.request();
        auto b_buf = b.request();
        if (a_buf.ndim != 2 || b_buf.ndim != 2) {
          throw std::runtime_error("int8 matmul expects 2D inputs");
        }
        int64_t m = a_buf.shape[0];
        int64_t k_a = a_buf.shape[1];
        int64_t k_b = b_buf.shape[0];
        int64_t n = b_buf.shape[1];
        if (k_a != k_b) {
          throw std::runtime_error("Inner dims must match");
        }
        py::array_t<float> out({m, n});
        // allocate device buffers
        int8_t *d_a = nullptr, *d_b = nullptr;
        float* d_out = nullptr;
        cudaMalloc(&d_a, m * k_a * sizeof(int8_t));
        cudaMalloc(&d_b, k_b * n * sizeof(int8_t));
        cudaMalloc(&d_out, m * n * sizeof(float));
        cudaMemcpy(d_a, a_buf.ptr, m * k_a * sizeof(int8_t), cudaMemcpyHostToDevice);
        cudaMemcpy(d_b, b_buf.ptr, k_b * n * sizeof(int8_t), cudaMemcpyHostToDevice);

        dim3 block(16, 16);
        dim3 grid((n + block.x - 1) / block.x, (m + block.y - 1) / block.y);
        MatmulInt8Kernel<<<grid, block>>>(d_a, d_b, d_out, m, k_a, n, a_scale, b_scale);

        cudaMemcpy(out.request().ptr, d_out, m * n * sizeof(float), cudaMemcpyDeviceToHost);
        cudaFree(d_a);
        cudaFree(d_b);
        cudaFree(d_out);
        return out;
      },
      py::arg("a"), py::arg("b"), py::arg("a_scale"), py::arg("b_scale"),
      "Int8 matmul on CUDA (a: m x k, b: k x n) returning float32.");
}
